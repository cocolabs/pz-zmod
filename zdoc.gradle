// directory containing generated lua library
project.ext.zDocLuaDir = file("$buildDir/generated/sources/zdoc/").absoluteFile

repositories {
    maven { url 'https://jitpack.io' }
}
configurations {
    zomboidDoc.extendsFrom zomboidRuntimeOnly
    zomboidDoc.extendsFrom zomboidImplementation
}
dependencies {
    // https://github.com/real-coco-labs/pz-zdoc
    zomboidDoc 'com.github.real-coco-labs:pz-zdoc:v2+'

    // ZomboidDoc compiled Lua library
    compileOnly files("lib/pz-zdoc-lua-$game_version" + '.jar')
}

tasks.register('zomboidLuaJar', ZDocJar.class) {
    it.description('Assembles a jar containing compiled lua classes.')

    it.archiveBaseName.set('zdoc-lua')
    it.from zDocLuaDir
}

tasks.register('zomboidVersion', JavaExec.class) {
    it.description('Read Project Zomboid game version.')
    it.group('zomboid')

    it.main = 'io.yooksi.pz.zdoc.Main'
    it.classpath = configurations.zomboidDoc
    it.args 'version'

    OutputStream oStream = new ByteArrayOutputStream()
    it.setStandardOutput(oStream)

    it.doLast {
        // get application command output from stream
        // ex. game version 41.50 - IWBUMS
        def versionText = oStream.toString()

        // get version number and classifier (ex. 41.50-IWBUMS)
        project.ext.gameVersion = versionText.substring(18).replaceAll(" ", "")
        println("game version: ${project.ext.gameVersion}")
    }
    it.dependsOn('zomboidClasses')
}

tasks.register('annotateZomboidLua', JavaExec.class) {
    it.description('Annotate vanilla Lua with EmmyLua.')
    it.setGroup('zomboid')

    it.main = 'io.yooksi.pz.zdoc.Main'
    it.classpath = configurations.zomboidDoc
    it.args('annotate', '-i', "$gameDir/media/lua", '-o', "$zDocLuaDir/media/lua")
    it.dependsOn('zomboidClasses')
}

tasks.register('compileZomboidLua', JavaExec.class) {
    it.description('Compile Lua library from modding API.')
    it.setGroup('zomboid')

    it.main = 'io.yooksi.pz.zdoc.Main'
    it.classpath = configurations.zomboidDoc
    it.args('compile', '-i', "$gameDir", '-o', "$zDocLuaDir/media/lua/shared/Library")
    it.dependsOn('zomboidClasses')
}
