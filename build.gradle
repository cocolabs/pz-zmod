plugins {
    // Apply the java plugin to add support for Java
    id 'java'

    // Facilitates building archives that serve as project distributions
    id 'distribution'
}
repositories {
    jcenter()
}

java {
    // ZomboidDoc can only be executed with Java 8
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}
apply from: 'setup.gradle'
if (project.ext.has('gameDir'))
{
    List<String> resourcesList = new ArrayList<>()
    def gameDirPath = gameDir as java.nio.file.Path
    //@formatter:off
    gameDirPath.resolve('media').toFile().listFiles().each
    {
        if (it.directory) {
            resourcesList.add("media/${it.name}")
        }
    }//@formatter:on
    sourceSets {
        media {
            java.srcDirs = ['media/lua']
            resources.srcDirs resourcesList
        }
    }
    apply from: 'mod.gradle'
    apply from: 'zomboid.gradle'
    apply from: 'zdoc.gradle'

    jar {
        from sourceSets.media.output
    }
    version modInfoProperties.getProperty('modversion')
    apply from: 'distribution.gradle'

    defaultTasks 'showModInfo'
}
