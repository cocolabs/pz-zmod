plugins {
	// Apply the java plugin to add support for Java
	id 'java'

	// Facilitates building archives that serve as project distributions
	id 'distribution'
}
repositories {
	mavenCentral()
}

java {
	// ZomboidDoc can only be executed with Java 8
	toolchain {
		languageVersion = JavaLanguageVersion.of(8)
	}
}
apply from: 'setup.gradle'
if (project.ext.has('gameDir'))
{
	Set<String> sourcesList = new HashSet<>(['media/lua'])
	Set<String> resourcesList = new HashSet<>()
	Set<String> excludeResources = new HashSet<>([
			'media/lua', 'media/luaexamples',
			'media/newuitests', 'media/launcher',
	])
	def gameDirPath = gameDir as java.nio.file.Path
	//@formatter:off
    gameDirPath.resolve('media').toFile().listFiles().each
    {
        if (it.directory)
        {
            String filepath = "media/${it.name}"
            if (!excludeResources.contains(filepath)) {
                resourcesList.add(filepath)
            }
        }
    }//@formatter:on
	sourceSets {
		media {
			java.srcDirs = sourcesList
			resources.srcDirs = resourcesList
		}
	}
	apply from: 'mod.gradle'
	apply from: 'zomboid.gradle'
	apply from: 'zdoc.gradle'

	jar.from sourceSets.media.output

	version modInfoProperties.getProperty('modversion')
	apply from: 'distribution.gradle'

	defaultTasks 'showModInfo'
}
